#nop ALIAS
#ALIAS {cpic}{2n;pic;2s}
#ALIAS {gpic}{yard;pic;yardb}
#ALIAS {p2}{#2 pick herb}
#ALIAS {pic}{2e;p2;e;p2;s;p2;s;p2;nw;p2;nww}

#read nanny.settings
#read nanny.alias

#var didsomething {0};
#action {** HP: %1/%2   SP: %3/%4}{
    #math {php}{(%1*100)/%2};
    #math {psp}{(%3*100)/%4};
    #var hp %1;
    #var sp %3;
    #if {$didsomething == 0}{
        #if {(%3*100)/%4 < 50}{
            #var didsomething 1;
            #delay {2}{#var didsomething 0};
            #delay {2}{heal me mentally with herb;}
        };
        #else {
            #if {(%1*100)/%2 < 90}{
                #var didsomething 1;
                #delay {2}{#var didsomething 0};
                #delay {2}{h};
            };
        };
    };
}

#high {[Monks]}{b white}
#high {[Lars' Word]}{b white}

#action {[Lars' Word] %0 will be in 1 minute in the monastery chapel.}{
    #var mass %0;
    #var mass_min {in 1 minute};
    #var {real_mass_min}{1};
    #cr;
}

#action {[Lars' Word] %0 will be in %1 minutes in the monastery chapel.}{
    #var mass %0;
    #var mass_min {in %1 minutes};
    #var {real_mass_min}{%1};
    #cr;
}

#action {[Lars' Word] %0 starts now.}{
    #var mass %0;
    #var mass_min {is now};
    #var {real_mass_min}{0};
    #cr;
}

#PROMPT {[xp%0][%1][%2][%3]}{[xp%0][%1][%2][%3] [$mass $mass_min]}
#action {You are blessed by Lars.}{pray to lars}

#action {You now feel ready to go out into the world.}{
    #var mass_min {just was};
    #var {real_mass_min}{180};
    #cr;
}

#high {Your faith is strong.}{tan}
#high {You feel the touch of Lars.}{tan}
#high {You feel the presence of Lars.}{tan}

#action {The next mass is in about %0 minutes.}{
    #var mass {Next mass};
    #var mass_min {in %0 minutes};
    #var {real_mass_min}{%0};
}

#action {Evening mass is in about %0 minutes.}{
    #var mass {Evening mass};
    #var mass_min {in %0 minutes};
    #var {real_mass_min}{%0};
}

#action {Mendel tells you: %1 is in %2}{
	#delay {3}{
		#var mass {%1};
		#var mass_min {in %2};
	}
}

#read nanny.monk.secret
#EVENT {PROGRAM START}{#ses {NannyMUD Monk}{130.236.254.159}{2000}}
#event {SESSION CONNECTED}{user_pass;Mreview}
#action {------------------------------ GENERAL INFO -------------------------------}
{
	2sw;
	withdraw all;
	en;
	yard;
	pic;
	s;
	read note;
	s;
	pray to lars;
	#delay {1}{#cr};
}
#alias owood #read nanny.monk.old_wood

#action {^[Party] %1: heal$}{heal %1}{3}
#action {^[Party] %1: heal p$}{heal %1 physical}{2}
#action {^[Party] %1: heal h$}{read heal;point %1}{2}

#action {Mendel tells you: Enjoy your time in the garden, }{#nop}{1}